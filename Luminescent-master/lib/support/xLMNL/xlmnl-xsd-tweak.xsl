<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  version="2.0"
  xmlns:xs="http://www.w3.org/2001/XMLSchema">
  
  <!-- For tweaking the "raw" xLMNL XSD (generated by Trang from
       the canonical RNC version of the xLMNL schema) into a
       more graceful form. -->
  
  <xsl:strip-space elements="*"/>
  
  <xsl:output indent="yes"/>
  
  <xsl:template match="comment()">
    <xsl:copy-of select="."/>
    <xsl:text>&#xA;</xsl:text>
  </xsl:template>
  
  <xsl:template match="* | @*">
    <xsl:copy>
      <xsl:apply-templates select="node() | @*"/>
    </xsl:copy>
  </xsl:template>
  
  <xsl:template match="/*">
    <xsl:comment>
      <xsl:text>&#xA;  XSD generated from RNC using Trang&#xA;  and tweaked with xlmnl-xsd-tweak.xsl, </xsl:text>
      <xsl:value-of select="format-date(current-date(),'[Y]-[M01]-[D01]. ')"/>
    </xsl:comment>
    <xsl:text>&#xA;&#xA;</xsl:text>
    <xsl:next-match/>
  </xsl:template>
  
  <xsl:template match="/*/*">
    <xsl:text>&#xA;&#xA;</xsl:text>
    <xsl:next-match/>
  </xsl:template>
  
  <xsl:template priority="2"
    match="*[exists(xs:attribute[@name=('sl','so','el','eo')])]">
    <xsl:copy>
      <xsl:copy-of select="@*"/>
      <xsl:apply-templates select="* except xs:attribute[@name=('sl','so','el','eo')]"/>
      <xs:attributeGroup ref="x:debug-support"/>
    </xsl:copy>
  </xsl:template>

  <xsl:template priority="3"
    match="xs:attributeGroup[@name='debug-support']">
    <xsl:text>&#xA;&#xA;</xsl:text>
    <xsl:copy>
      <xsl:apply-templates select="node() | @*"/>
    </xsl:copy>
  </xsl:template>
  
  <xsl:template match="xs:attributeGroup[@name='debug-support']/xs:attribute/@use"/>
  
  
</xsl:stylesheet>